apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
}


// Bintray Setup
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ['MobileORM']

    pkg {
        repo = 'support'
        name = 'mobile-orm'

        userOrg = 'odooongobox'
        desc = 'Mobile ORM (SQLite) for Android'

        websiteUrl = 'http://www.oogbox.com'
        issueTrackerUrl = 'https://github.com/oogbox/mobile-orm/issues'
        vcsUrl = 'https://github.com/oogbox/mobile-orm.git'

        licenses = ["AGPL-V3"]
        publish = true
        publicDownloadNumbers = true

        version {
            name = "1.0.0"
            desc = "Mobile ORM (SQLite) for Android"
        }
    }
}

publishing {
    publications {
        MobileORM(MavenPublication) {
            from components.android
            groupId 'com.oogbox.support'
            artifactId 'mobile-orm'
            version '1.0.0'
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}